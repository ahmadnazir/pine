OPERATIONS := OPERATION <whitespace*> (<"|"> <whitespace*> OPERATION <whitespace*>)*
OPERATION  := SELECT | CONDITION | LIMIT | GROUP | ORDER | META | DELETE | SET

SELECT          := specific | invert-specific
specific        := <#"s(el(ect)?)?:"> <whitespace+> columns
invert-specific := <#"u(ns(elect)?)?:"> <whitespace+> columns
columns         := column (<","> <whitespace*> column)*
column          := string | string <whitespace*> <#"as|AS"> <whitespace*> alias
alias           := string

CONDITION       := entity | entity <whitespace+> id | entity <whitespace+> comparisons


ORDER           := <#"o(rder)?:"> <whitespace*> "+"? column

GROUP           := <#"g(roup)?:"> <whitespace+> column (<whitespace+> group-fn)?
group-fn        := #"(count|min|max|join)" <":"> <whitespace+> column

LIMIT           := <#"l(imit)?:"> <whitespace+> number

META            := ref
ref             := <#"ref(erences)?"> <"?">

DELETE          := <#"delete!">

SET             := <#"set!"> <whitespace+> assignments

id           := string
assignments  := assignment (<whitespace+> assignment)*
assignment   := string <whitespace*> <"="> <whitespace*> string

comparisons  := comparison (<whitespace+> comparison)*
comparison   := string <whitespace*> operator <whitespace*> string


number     := #"[0-9]+"
string     := #"[A-Za-z0-9-_*.@]+"
whitespace := #"\s+"
entity     := string
operator   := #"<|>|="
