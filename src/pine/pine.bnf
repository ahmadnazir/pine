OPERATIONS := OPERATION <whitespace*> (<"|"?> <whitespace*> OPERATION <whitespace*>)*
OPERATION  := SELECT | CONDITION | LIMIT | FUNCTION | GROUP | ORDER | META | DELETE

SELECT          := specific | invert-specific
specific        := <#"s(el(ect)?)?:"> <whitespace+> columns
invert-specific := <#"u(ns(elect)?)?:"> <whitespace+> columns
columns         := column (<","> <whitespace*> column)*
column          := string

CONDITION       := entity | entity <whitespace+> filters
entity          := string
filters         := filter (<whitespace+> filter)*
filter          := implicit-filter | explicit-filter
implicit-filter := string
explicit-filter := string <whitespace*> <"="> <whitespace*> string

FUNCTION        := #"(count|min|max)" <":"> <whitespace+> columns

ORDER           := <#"o(rder)?:"> <whitespace*> "+"? column

GROUP           := <#"g(roup)?:"> <whitespace+> columns

LIMIT           := <#"l(imit)?:"> <whitespace+> number

META            := #"(references)" <"?">

DELETE          := <#"delete!">

number     := #"[0-9]+"
string     := #"[A-Za-z0-9-_*.@]+"
whitespace := #"\s+"
