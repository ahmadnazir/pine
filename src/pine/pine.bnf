OPERATIONS := OPERATION <whitespace*> (<"|"> <whitespace*> OPERATION <whitespace*>)*
OPERATION  := SELECT | CONDITION | LIMIT | FUNCTION | GROUP | ORDER | META | DELETE | SET

SELECT          := specific | invert-specific
specific        := <#"s(el(ect)?)?:"> <whitespace+> columns
invert-specific := <#"u(ns(elect)?)?:"> <whitespace+> columns
columns         := column (<","> <whitespace*> column)*
column          := string

CONDITION       := entity | entity <whitespace+> values

FUNCTION        := #"(count|min|max)" <":"> <whitespace+> columns

ORDER           := <#"o(rder)?:"> <whitespace*> "+"? column

GROUP           := <#"g(roup)?:"> <whitespace+> columns

LIMIT           := <#"l(imit)?:"> <whitespace+> number

META            := ref
ref             := <#"ref(erences)?"> <"?">

DELETE          := <#"delete!">

SET             := <#"set!"> <whitespace+> key-values

number     := #"[0-9]+"
string     := #"[A-Za-z0-9-_*.@]+"
whitespace := #"\s+"
entity     := string

values      := id | key-values
id          := string
key-values  := key-value (<whitespace+> key-value)*
key-value   := string <whitespace*> <"="> <whitespace*> string