OPERATIONS := OPERATION <whitespace*> (<"|"> <whitespace*> OPERATION <whitespace*>)*
OPERATION  := SELECT | RESOURCE | LIMIT | GROUP | ORDER | META | DELETE | SET

SELECT          := specific | invert-specific
specific        := <#"s(el(ect)?)?:"> <whitespace+> columns
invert-specific := <#"u(ns(elect)?)?:"> <whitespace+> columns
columns         := column (<","> <whitespace*> column)*
column          := string | string <whitespace*> <#"as|AS"> <whitespace*> alias
alias           := string

RESOURCE        := entity | entity <whitespace+> id | entity <whitespace+> ids | entity <whitespace+> ands | entity <whitespace+> ors

ORDER           := <#"o(rder)?:"> <whitespace*> "+"? column

GROUP           := <#"g(roup)?:"> <whitespace+> column (<whitespace+> group-fn)?
group-fn        := #"(count|min|max|join)" <":"> <whitespace+> column

LIMIT           := <#"l(imit)?:"> <whitespace+> number

META            := ref
ref             := <#"ref(erences)?"> <"?">

DELETE          := <#"delete!">

SET             := <#"set!"> <whitespace+> assignments

id           := string
assignments  := assignment (<whitespace+> assignment)*
assignment   := string <whitespace*> <"="> <whitespace*> string

ids         := string (<whitespace*> <","> <whitespace*> string)*
ors         := comparison (<whitespace*> <","> <whitespace*> comparison)*
ands        := comparison (<whitespace*> comparison)*
comparison  := string <whitespace*> operator <whitespace*> string | string "?" | "!" string


number     := #"[0-9]+"
string     := #"[A-Za-z0-9-_*.@]+"
whitespace := #"\s+"
entity     := string
operator   := #"<|>|="
